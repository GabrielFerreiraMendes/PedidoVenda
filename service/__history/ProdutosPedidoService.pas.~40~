unit ProdutosPedidoService;

interface

uses ProdutosPedido, FireDAC.Comp.Client, uDmPrincipal, System.SysUtils,
  System.Generics.Collections;

type
  TProdutosPedidoService = class
  public
    class procedure save(ProdutosPedido: TProdutosPedido);

    class function getByNumeroPedido(NumeroPedido: String)
      : TObjectList<TProdutosPedido>;

  end;

implementation

{ TProdutosPedidoService }

class function TProdutosPedidoService.getByNumeroPedido(NumeroPedido: String)
  : TObjectList<TProdutosPedido>;
var
  qry: TFDQuery;
begin
  qry := TFDQuery.Create(nil);

  try
    qry.Connection := dmPrincipal.FDConnection1;

    qry.Close;
    qry.SQL.Clear;
    qry.SQL.Add('SELECT * FROM PRODUTOS_PEDIDO          ');
    qry.SQL.Add('WHERE NUMERO_PEDIDO = :NUMERO_PEDIDO;  ');
    qry.ParamByName('NUMERO_PEDIDO').AsString := NumeroPedido;
    qry.Open;

    if qry.IsEmpty then
    begin
      Result := nil;

      Exit;
    end;

    Result := TObjectList<TProdutosPedido>.Create;

    while not qry.Eof do
    begin
      Result.Add(TProdutosPedido.Create(qry.FieldByName('NUMERO_PEDIDO')
        .AsInteger, qry.FieldByName('CODIGO_PRODUTO').AsInteger,
        qry.FieldByName('QUANTIDADE').AsInteger,
        qry.FieldByName('VALOR_UNITARIO').AsCurrency,
        qry.FieldByName('VALOR_TOTAL').AsCurrency));

      qry.Next;
    end;

  finally
   qry.Close;
   FreeAndNil(qry);
  end;

end;

class procedure TProdutosPedidoService.save(ProdutosPedido: TProdutosPedido);
var
  qry: TFDQuery;
  NumeroPedido: Integer;
begin
  qry := TFDQuery.Create(nil);

  try
    qry.Connection := dmPrincipal.FDConnection1;

    qry.Close;
    qry.SQL.Clear;
    qry.SQL.Add('INSERT INTO PRODUTOS_PEDIDO(NUMERO_PEDIDO,  ');
    qry.SQL.Add('                            CODIGO_PRODUTO, ');
    qry.SQL.Add('                            QUANTIDADE,     ');
    qry.SQL.Add('                            VALOR_UNITARIO, ');
    qry.SQL.Add('                            VALOR_TOTAL)    ');
    qry.SQL.Add('VALUES(:NUMERO_PEDIDO,                      ');
    qry.SQL.Add('       :CODIGO_PRODUTO,                     ');
    qry.SQL.Add('       :QUANTIDADE,                         ');
    qry.SQL.Add('       :VALOR_UNITARIO,                     ');
    qry.SQL.Add('       :VALOR_TOTAL);                       ');
    qry.ParamByName('NUMERO_PEDIDO').AsInteger := ProdutosPedido.NumeroPedido;
    qry.ParamByName('CODIGO_PRODUTO').AsInteger := ProdutosPedido.CodigoProduto;
    qry.ParamByName('QUANTIDADE').AsInteger := ProdutosPedido.Quantidade;

    qry.ParamByName('VALOR_UNITARIO').AsCurrency :=
      ProdutosPedido.ValorUnitario;

    qry.ParamByName('VALOR_TOTAL').AsCurrency := ProdutosPedido.ValorTotal;
    qry.ExecSQL;
  finally
    qry.Close;
    FreeAndNil(qry);
  end;
end;

end.
